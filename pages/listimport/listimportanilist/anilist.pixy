component ImportAnilist(user *arn.User, matches []*arn.AniListMatch)
	h1= "Preview: anilist.co (" + user.Accounts.AniList.Nick + ", " + fmt.Sprint(len(matches)) + " anime)"

	ImportButton("/import/anilist/animelist/finish")

	table.import-list
		thead
			tr
				th anilist.co
				th notify.moe
		tbody
			each match in matches
				tr
					td
						a(href=match.AniListItem.Anime.Link(), target="_blank", rel="noopener")= match.AniListItem.Anime.Title.Romaji
					td
						if match.ARNAnime == nil
							span.import-error Not found on notify.moe
						else
							a(href=match.ARNAnime.Link(), target="_blank", rel="noopener")= match.ARNAnime.Title.Canonical
	
	ImportButton("/import/anilist/animelist/finish")