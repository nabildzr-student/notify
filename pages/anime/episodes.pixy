component AnimeEpisodes(anime *arn.Anime, episodes []*arn.Episode, episodeToFriends map[int][]*arn.User, user *arn.User, standAlonePage bool)
	if standAlonePage
		h1.mountable
			a(href=anime.Link())= anime.Title.ByUser(user)
	
	if len(episodes) > 0
		.anime-section.mountable
			h3.anime-section-name
				a(href=fmt.Sprintf("/anime/%s/episodes", anime.ID)) Episodes
			
			.episodes
				each episode in episodes
					a.episode.mountable(href=episode.Link(), data-mountable-type="episode", data-available=episode.Available())
						.episode-number
							if episode.Number != -1
								span= episode.Number
						
						if episode.Title.Japanese != ""
							span.episode-title= episode.Title.Japanese
						else
							span.episode-title -
						
						if validate.DateTime(episode.AiringDate.Start)
							time.episode-airing-date-start.utc-airing-date.no-tip(datetime=episode.AiringDate.Start, data-end-date=episode.AiringDate.End, data-episode-number=episode.Number)= episode.AiringDate.StartDateHuman()
						
						//- Show friends in reverse order
						.episode-friends
							each friend in episodeToFriends[episode.Number] reversed
								AvatarNoLink(friend)
